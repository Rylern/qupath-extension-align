plugins {
  id 'java-library'
  // To create a shadow/fat jar, including dependencies
  // (not very useful here)
  id 'com.github.johnrengelman.shadow' version '7.0.0'
}

repositories {
  mavenCentral()
  
  maven {
    url "https://maven.scijava.org/content/repositories/releases"
  }
  
  maven {
    url "https://maven.scijava.org/content/repositories/snapshots"
  }
  
}

ext.moduleName = 'qupath.extension.align'

description = 'QuPath extension for interactive image alignment'

version = "0.3.0-SNAPSHOT"

dependencies {
    shadow "io.github.qupath:qupath-gui-fx:0.3.0-SNAPSHOT"
    shadow "org.slf4j:slf4j-api:1.7.30"
}

processResources {
  from ("${projectDir}/LICENSE.txt") {
    into 'licenses/'
  }
}

// Copy dependencies
task copyDependencies(type: Copy) {
  from configurations.default
  into 'build/libs'
}

java {
    def version = project.findProperty('toolchain')
    if (!version)
        version = 11
    else if (version.strip() == 'skip')
        version = null
    if (version != null) {
        logger.quiet("Setting toolchain to {}", version)
        toolchain {
            languageVersion = JavaLanguageVersion.of(version)
        }
    }
}
